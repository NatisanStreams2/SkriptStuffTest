# MADE BY NATISAN (optional support donation: https://ko-fi.com/natisan)

# Economy skript
# This skript adds a simple economy system with commands like /eco and /ecoadmin (feel free to change the name of the commands as you see fit)

# REQUIRED ADDONS:
#   PlaceholderAPI (IF YOU WANT THE PLACEHOLDERS, if not, delete the placeholders lines)
#   SkBee


placeholderapi placeholder with the prefix "skript":
    if the identifier is "balance":
        set the result to {balance::%player's uuid%}


function getBalance(p: offlineplayer):: integer:
    return {balance::%{_p}'s uuid%}

function addBalance(p: offlineplayer, amount: integer):
    add {_amount} to {balance::%{_p}'s uuid%}

function removeBalance(p: offlineplayer, amount: integer):
    remove {_amount} from {balance::%{_p}'s uuid%}

function setBalance(p: offlineplayer, amount: integer):
    set {balance::%{_p}'s uuid%} to {_amount}


options:
    starter_balance: 0 # the amount of coins you want people to start with 
    name: <white>Coins # The name of the currency
    prefix: <white>[<yellow>ECONOMY<white>] # the prefix that shows up for messages
    no: play sound "entity.villager.no" with volume 0.5 # OPTIONAL error sound (delete if you dont want it and delete the "{@no} to player" lines)
    baltopcount: 10 # The amount of players to show in /baltop

# Detects if the player has a balance set, if not, it'll give them the starter balance (mainly for first joins)
on join:
    if {balance::%player's uuid%} is not set:
        set {balance::%player's uuid%} to {@starter_balance}


# Commands:
# /eco balance - shows the player's balance
# /eco pay <player> <amount> - pays another player
command /eco [<text>] [<player>] [<integer>]:
    trigger:
        if arg-1 is "balance":
            send "{@prefix} <white>Your balance is %{balance::%player's uuid%}% {@name}" to player
        if arg-1 is "pay":
            if arg-2 is set:
                if arg-3 is set:
                    if arg-3 is less than or equal to {balance::%player's uuid%}:
                        if arg-3 is more than 0:
                            removeBalance(player, arg-3)
                            addBalance(arg-2, arg-3)
                            send "{@prefix} <white>You have sent <yellow>%arg-3% {@name} <white>to <cyan>%arg-2%" to player
                            send "{@prefix} <white>You have received <yellow>%arg-3% {@name} <white>from <cyan>%player%" to arg-2
                            play sound "entity.experience_orb.pickup" with volume 0.5 to player
                            play sound "entity.experence_orb.pickup" with volume 0.5 to arg-2
                        else:
                            send "{@prefix} <red>You cannot send negative numbers" to player
                            {@no} to player
                    else:
                        send "{@prefix} <red>You don't have enough money to send this much" to player
                        {@no} to player

on tab complete of "eco":
    set tab completion for position 1 to "balance", "pay"

# Admin commands:
# /ecoadmin add <player> <amount> - adds money to a player's balance
# /ecoadmin remove <player> <amount> - removes money from a player's balance
# /ecoadmin set <player> <amount> - sets a player's balance
# /ecoadmin reset <player> - resets a player's balance to the starter balance option
command /ecoadmin [<text>] [<player>] [<integer>]:
    permission: eco.admin
    trigger:
        if arg-1 is "add":
            if arg-2 is set:
                if arg-3 is set:
                    if arg-3 > 0:
                        addBalance(arg-2, arg-3)
                        send "{@prefix} Added <yellow>10 {@name} <white>to <cyan>%arg-2%'s <white>balance"
                    else:
                        send "{@prefix} <red>You can't add a negative number"
                        {@no} to player
                else:
                    send "{@prefix} <red>Please set an amount"
                    {@no} to player
        if arg-1 is "set":
            if arg-2 is set:
                if arg-3 is set:
                    setBalance(arg-2, arg-3)
                    send "{@prefix} You have set <cyan>%arg-2%'s <white>balance to <yellow>%arg-3% {@name}"
                else:
                    send "{@prefix} <red>Please set an amount"
                    {@no} to player
        if arg-1 is "remove":
            if arg-2 is set:
                if arg-3 is set:
                    if arg-3 > 0:
                        removeBalance(arg-2, arg-3)
                        send "{@prefix} You have removed <yellow>%arg-3% {@name} <white>from <cyan>%arg-2%'s <white>balance"
                    else:
                        send "{@prefix} <red>You can't remove a negative number"
                        {@no} to player
                else:
                    send "{@prefix} <red>Please set an amount"
                    {@no} to player
        if arg-1 is "reset":
            if arg-2 is set:
                set {balance::%arg-2's uuid%} to {@starter_balance}
                send "{@prefix} You have reset <cyan>%arg-2%'s <white>balance"
            else:
                send "{@prefix} <red>Please set a player"
                {@no} to player

on tab complete of "ecoadmin":
    set tab completion for position 1 to "add", "remove", "set", "reset"
                

# /baltop - shows the top balances - edit the "baltopcount" option to change how many players show
command /baltop:
    trigger:
        set {_sorted::*} to sorted indices of {balance::*} in descending order
        send "<cyan>---Baltop---"
        loop first {@baltopcount} elements of {_sorted::*}:
            send "<gold>%loop-iteration%. <white>%offlineplayer(loop-value)% <gray>- <lime>$%{balance::%loop-value%}%"